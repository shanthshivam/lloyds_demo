buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.h2database:h2:1.3.170'
    classpath 'junit:junit:4.11'		    
  }
}
apply plugin: 'war'

repositories {
	mavenCentral()
}

dependencies {
	providedCompile "javax.servlet:servlet-api:2.5"
	compile("commons-io:commons-io:2.4")
	compile 'javax.inject:javax.inject:1'
	testCompile 'junit:junit:4.11'
	runtime 'com.h2database:h2:1.3.170'
}
war{
	baseName = "todo"
	version = "1.0"
	extension = "war"
	includeEmptyDirs = false
}
apply from: 'databaseSetup.gradle'

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

check.dependsOn integrationTest

